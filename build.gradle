buildscript {
    ext.kotlin_version = '1.9.20'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}

// these are not release numbers. release numbers are in deploy.settings files.
ext.ver = [
        "hmkit-crypto-telematics": "0.1",
        "hmkit-utils"            : "1.4.5",
        "hmkit-auto-api"         : "13.1.1",
        "compileSdk"             : 28
]

ext.deps = [
        "utils"  : "com.high-mobility:hmkit-utils:${ver."hmkit-utils"}",
        "autoApi": "com.high-mobility:hmkit-auto-api:${ver."hmkit-auto-api"}",
]

apply plugin: "java-library"
apply plugin: 'kotlin'
apply from: "deploy.gradle"
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/deploy-java-mavenCentral.gradle"
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/test-shell-logging.gradle"

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.bouncycastle:bcprov-jdk18on:1.76"

    if (project.hasProperty('depLocation') == false || project.depLocation > 0) {
        api deps.utils
    } else {
        api project(':hmkit-utils')
    }

    testImplementation 'junit:junit:4.12'
    testImplementation deps.autoApi
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}